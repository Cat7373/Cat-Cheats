plugins {
    id "net.minecraftforge.gradle.forge" version "2.0.2"
}

ext.modversion = "${project.version_major}.${project.version_minor}.${project.version_micro}"

version = "${project.version_minecraft}-${modversion}"
group = 'org.cat73.cheats'
archivesBaseName = "Cat-Cheats"

compileJava.options.encoding = 'UTF-8'

sourceSets.main{
    java{
        srcDirs 'src'
    }
    resources{
        srcDirs 'resources'
    }
}

minecraft {
    version = "${project.version_minecraft}-${project.version_forge}"
    if (project.hasProperty('version_mappings'))
        mappings = project.version_mappings
        
    replaceIn 'reference/Reference.java'
    replace '${version}', modversion
}

processResources {
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand([
                'version'        : modversion,
                'mcversion'      : project.version_minecraft,
                'minforgeversion': project.hasProperty('version_minforge') ? project.version_minforge : project.version_forge,
        ])
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
